;;; SPDX-FileCopyrightText: 2024 Jomco B.V.
;;; SPDX-FileCopyrightText: 2024 Topsector Logistiek
;;; SPDX-FileContributor: Joost Diepenmaat <joost@jomco.nl>
;;; SPDX-FileContributor: Remco van 't Veer <remco@jomco.nl>
;;;
;;; SPDX-License-Identifier: AGPL-3.0-or-later

(ns dil-demo.wms.web-test
  (:require [clojure.test :refer [deftest is testing]]
            [dil-demo.store :as store]
            [dil-demo.wms.web :as sut]
            [nl.jomco.http-status-codes :as http-status]
            [ring.mock.request :refer [request]]))

(def store
  {:transport-orders
   {"31415"
    {:id "31415"
     :ref "31415"}}})

(defn do-request [method path & [params]]
  ((sut/make-handler {:id :wms, :site-name "WMS"})
   (assoc (request method path params)
          ::store/store store
          :user-number 1)))

(deftest handler
  (testing "GET /"
    (let [{:keys [status headers]} (do-request :get "/")]
      (is (= http-status/ok status))
      (is (= "text/html; charset=utf-8" (get headers "Content-Type")))))

  (testing "DELETE /transport-order-31415"
    (let [{:keys [status ::store/commands]} (do-request :delete "/transport-order-31415")]
      (is (= http-status/see-other status))
      (is (= [:delete! :transport-orders] (->> commands first (take 2))))))

  (testing "GET /verify-not-found"
    (is (nil? (do-request :get "/verify-not-found"))))

  (testing "GET /verify-31415"
    (let [{:keys [status headers body]} (do-request :get "/verify-31415")]
      (is (= http-status/ok status))
      (is (= "text/html; charset=utf-8" (get headers "Content-Type")))
      (is (re-find #"<input [^>]*\bvalue=\"31415\"[^>]*>" body))))

  (testing "POST /verify-31415"
    "TODO, this calls out to satellite and ARs"))
